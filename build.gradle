
group 'com.github.mustard.chatterbox'
version '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}


buildscript {

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
    }
}


subprojects {

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://palantir.bintray.com/releases" }
    }

    apply plugin: 'java'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'com.github.ben-manes.versions'

    sourceCompatibility = 1.7

    dependencies {
        testCompile group: 'org.junit.jupiter',
                    name: 'junit-jupiter-api',
                    version: "$junitVersion"

        testRuntime group: 'org.junit.jupiter',
                    name: 'junit-jupiter-engine',
                    version: "$junitVersion"
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

}


project('ms-bot:ms-bot-domain') {

//    apply from: '../gradle/maven-publish.gradle'

    compileJava {
        options.compilerArgs += ["-parameters"]
    }

    dependencies {

        compile group: 'com.fasterxml.jackson.core',
                name: 'jackson-annotations',
                version: "$jacksonVersion"
    }

}

project('ms-bot:ms-bot-dropwizard') {

    dependencies {

        compile project(':ms-bot:ms-bot-webhook')

        compile group: 'io.dropwizard',
                name: 'dropwizard-core',
                version: "$dropwizardVersion"

    }

}

project('ms-bot:ms-bot-webhook') {

    dependencies {

        compile project(':ms-bot:ms-bot-domain')

        compile group: 'org.slf4j',
                name: 'slf4j-api',
                version: "$slf4jVersion"

        compile group: 'javax.servlet',
                name: 'javax.servlet-api',
                version: "$servletAPIVersion"

        compile group: 'com.fasterxml.jackson.core',
                name: 'jackson-databind',
                version: "$jacksonVersion"

        compile group: 'com.fasterxml.jackson.module',
                name: 'jackson-module-parameter-names',
                version: "$jacksonVersion"

        testCompile group: 'io.dropwizard',
                name: 'dropwizard-testing',
                version: "$dropwizardVersion"

        testCompile group: 'org.apache.httpcomponents',
                name: 'fluent-hc',
                version: "$httpComponentsVersion"

    }

}

project('slack:slack-domain') {

//    apply from: '../gradle/maven-publish.gradle'

    compileJava {
        options.compilerArgs += ["-parameters"]
    }

    dependencies {

        compile group: 'com.fasterxml.jackson.core',
                name: 'jackson-annotations',
                version: "$jacksonVersion"
    }

}



project('slack:slack-webhook') {

    dependencies {

        compile project(':slack:slack-domain')

        compile group: 'org.slf4j',
                name: 'slf4j-api',
                version: "$slf4jVersion"

        compile group: 'javax.servlet',
                name: 'javax.servlet-api',
                version: "$servletAPIVersion"

        compile group: 'com.fasterxml.jackson.core',
                name: 'jackson-databind',
                version: "$jacksonVersion"

        compile group: 'com.fasterxml.jackson.module',
                name: 'jackson-module-parameter-names',
                version: "$jacksonVersion"

        testCompile group: 'io.dropwizard',
                name: 'dropwizard-testing',
                version: "$dropwizardVersion"

        testCompile group: 'org.apache.httpcomponents',
                    name: 'fluent-hc',
                    version: "$httpComponentsVersion"

    }

}


project('slack:slack-client') {

    dependencies {

        compile project(':slack:slack-domain')

        compile "com.palantir.roboslack:roboslack-webhook:1.2.0"

    }

}

project('slack:slack-dropwizard') {

    dependencies {

        compile project(':slack:slack-webhook')

        compile group: 'io.dropwizard',
                name: 'dropwizard-core',
                version: "$dropwizardVersion"
    }

}


project('example') {

    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'

    shadowJar {
        mergeServiceFiles()
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
//        transform(AppendingTransformer) {
//            resource = 'META-INF/cxf/bus-extensions.txt'
//        }
        // baseName = 'reception-app'
    }

    mainClassName = 'com.github.mustard.chatterbox.example.ExampleApp'


    dependencies {

        compile project(':slack:slack-dropwizard')
        compile project(':ms-bot:ms-bot-dropwizard')

        compile group: 'io.dropwizard',
                name: 'dropwizard-core',
                version: "$dropwizardVersion"

    }
}

configure([
        project(':ms-bot:ms-bot-webhook'),
        project(':slack:slack-domain'),
        project(':slack:slack-webhook'),
        project(':slack:slack-dropwizard')
]) {

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    publishing {
        repositories {
            mavenLocal()
        }
        publications {
            mavenJava(MavenPublication) {
                groupId "${rootProject.group}"
//                artifactId "${project.name}"
//                artifactId "com.github.mustard.chatterbox"
                version "${rootProject.version}"
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}